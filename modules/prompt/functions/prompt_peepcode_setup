#
# A simple theme from PeepCode.
# http://peepcode.com/blog/2012/my-command-line-prompt
#
# Authors:
#   Geoffrey Grosenbach <boss@topfunky.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/LhgmW.png
#

# todo: move to module
function whowhere-info {
  typeset -gA whowhere_info
  # don't username@host if i'm my usual user on my usual machine
  if [[ "$USERNAME@$HOST" != "$HIDE_WHOWHERE" ]] || [[ -n "$SSH_CONNECTION" ]]; then
    whowhere_info[info]="%B%F{yellow}[$USERNAME@$HOST]%f%b"
  else
    whowhere_info[info]=''
  fi
}


function prompt_peepcode_precmd {
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get Ruby information.
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi

  # Get Python information.
  if (( $+functions[python-info] )); then
    python-info
  fi

  # Get Node information.
  if (( $+functions[node-info] )); then
    node-info
  fi

  whowhere-info
}

function prompt_peepcode_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add a hook for calling info functions before each command.
  add-zsh-hook precmd prompt_peepcode_precmd

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'no'
  zstyle ':prezto:module:git:info:action' format ' +%s'
  zstyle ':prezto:module:git:info:branch' format ' %F{8}%b%f'
  zstyle ':prezto:module:git:info:commit' format ' %F{white}%.7c%f'
  zstyle ':prezto:module:git:info:indexed' format ' '
  zstyle ':prezto:module:git:info:unindexed' format ' '
  zstyle ':prezto:module:git:info:untracked' format ' '
  zstyle ':prezto:module:git:info:dirty' format ' %F{8}✗%f'
  zstyle ':prezto:module:git:info:keys' format 'rprompt' '%b%c%s%D'

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format ' %F{white}%v%f'

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '%F{white}ve:%v%f'

  zstyle ':prezto:module:node:info:version' format ' %F{white}nvm:%v%f'

  # Define prompts.
  if [[ $UID -eq 0 ]]; then
    PROMPT_SYMBOL='#'
  else
    PROMPT_SYMBOL='$'
  fi
  PROMPT='
%~
${whowhere_info[info]}%(?.%F{green}'${PROMPT_SYMBOL}'%f.%F{red}'${PROMPT_SYMBOL}'%f) '
RPROMPT='${python_info[virtualenv]}${ruby_info[version]}${node_info[version]}${git_info[rprompt]}'

}

function prompt_peepcode_help {
  cat <<EOH
This prompt's last command exit status symbol is customizable:

  prompt peepcode [<symbol>]

If this option is not provided, the symbol defaults to ☻.
EOH
}

function prompt_peepcode_preview {
  local +h PROMPT='%# '
  local +h RPROMPT=''
  local +h SPROMPT=''

  prompt_preview_theme 'peepcode'
}

prompt_peepcode_setup "$@"
